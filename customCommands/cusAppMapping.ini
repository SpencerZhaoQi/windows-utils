@echo off

rem 设置命令标识与路径

setlocal ENABLEDELAYEDEXPANSION

cd /d "%~dp0"

echo [zq]$ ______________________________________________________________________________________________________
echo [zq]$ _  V2.0 designed by zhaoqi at 2016 
echo [zq]$ _
echo [zq]$ _      #####     ##########    ############  ####      ###     #######   ############  ##########   
echo [zq]$ _   ##########   ############  ############# #####     ###   ######  ### ############# ############ 
echo [zq]$ _  ####          ###       ### ###           ######    ###  ####         ###           ###       ###
echo [zq]$ _  ###           ###       ### ###           ### ###   ### ####          ###           ###       ###
echo [zq]$ _  ############  ############  ############  ###  ###  ### ###           ############  ############ 
echo [zq]$ _   ############ ##########    ############  ###   ####### ###           ############  ##########   
echo [zq]$ _            ### ###           ###           ###    ###### ####          ###           ######        
echo [zq]$ _           #### ###           ###           ###     #####  ####         ###           ###  ###     
echo [zq]$ _   ###########  ###           ############# ###      ####   ######  ### ############# ###    ####   
echo [zq]$ _      ######    ###           ############  ###       ###     #######   ############  ###      #### 
echo [zq]$ _______________________________________________________________________________________________________



rem --------------------------------------------------------------------------------------------------------------
rem 从配置文件中读取内容
:InitConfigFile
rem set ROOT=%cd%
set MAIN_FILE=%~dp0\cus.bat
rem 命令路径配置文件先从用户目录取，如果没有则从命令执行的当前目录取
set CONFIG_FILE=%UserProfile%\cusAppMapping.ini
if not exist %CONFIG_FILE% (
	set CONFIG_FILE=%~dp0\cusAppMapping.ini
) 
echo [zq]$ currently use %CONFIG_FILE%
rem 设置固有命令
set inherent=ls,edit,editCommands,admin,exit
set commands=ls
FOR /F "tokens=1,2 delims===" %%i in (%CONFIG_FILE%) DO (
 set %%i=%%j
 set commands=!commands!,%%i
)
set all=!inherent!,!commands!
echo !jira!

rem --------------------------------------------------------------------------------------------------------------
rem 设置应用程序执行次数，默认为1
set runTimes=1

rem 如果执行时指定了应用参数，则直接打开参数所对应的目录
set opr=%1
set num=%2

if defined opr (
	set operate=%1
	rem 如果为固有命令，则进入命令选择程序，如果为其他命令，则进入应用打开关闭程序
	echo %inherent%|findstr "%1" >nul && (
		goto SelectCmdExecute
	) || (
		rem 指定要运行程序多少次(对应第二个参数，默认为1)
		if defined num (
			set runTimes=%2
		)
		goto OpenAndExit
	)
	
)


rem --------------------------------------------------------------------------------------------------------------
rem 命令列表
:ShowOperations
echo [zq]$ * Currently available operations : 
echo [zq]$ *	-- ls		(shows all commands)
echo [zq]$ *	-- edit		(edit this custom commands script,this operation needs to restart the script)
echo [zq]$ *	-- editCommands	(edit commands map file, and reload the file automatelly)
echo [zq]$ *	-- admin	(run as administrator)
echo [zq]$ *	-- ***		(custom commands)
echo [zq]$ *	-- exit		(exit system)
goto EnterTip


rem --------------------------------------------------------------------------------------------------------------
rem 提示输入命令
:EnterTip
echo [zq]$ Please select and the click the return button：
goto SelectCmdMain


rem --------------------------------------------------------------------------------------------------------------
rem 命令选择
:SelectCmdMain
set operate=
set /p operate=
goto SelectCmdExecute

:SelectCmdExecute
if "%operate%" equ ""			(goto SelectCmdMain)
if "%operate%" equ "?"			(goto ShowOperations)
if "%operate%" equ "help"		(goto ShowOperations)
if "%operate%" equ "ls"			(goto ShowCmds)
if "%operate%" equ "edit"		(goto EditScript)
if "%operate%" equ "editCommands"	(goto EditCommandsMap)
if "%operate%" equ "admin"		(goto admin)
if "%operate%" equ "exit"		(goto ExitCus)
goto OpenApp


rem --------------------------------------------------------------------------------------------------------------
rem 显示当前所支持的所有命令
:ShowCmds
echo [zq]$ The command supported at present are:
echo [zq]$ 	-- %all%
goto EnterTip


rem --------------------------------------------------------------------------------------------------------------
rem 退出命令界面
:ExitCus
exit


rem --------------------------------------------------------------------------------------------------------------
rem 编辑本脚本
:EditScript
echo [zq]$ The main script edit started...
notepad %MAIN_FILE%
echo [zq]$ The main script edited...
goto ShowCmds


rem --------------------------------------------------------------------------------------------------------------
rem 编辑本脚本
:EditCommandsMap
echo [zq]$ The %CONFIG_FILE% edit started...
notepad %CONFIG_FILE%
echo [zq]$ The %CONFIG_FILE% edited...
goto InitConfigFile


rem --------------------------------------------------------------------------------------------------------------
rem 使用管理员运行
:admin
if exist "%SystemRoot%\SysWOW64" path %path%;%windir%\SysNative;%SystemRoot%\SysWOW64;%~dp0
bcdedit >nul
if '%errorlevel%' NEQ '0' (
	goto adminExecute
) else (
	echo [zq]$ This window has already run as administrator
	rem 返回脚本目录
	cd /d "%~dp0"
)
goto ShowCmds

rem 切换管理员窗口
:adminExecute
rem %1 start "" mshta vbscript:createobject("shell.application").shellexecute("""%~0""","::",,"runas",1)(window.close)&exit
start "" mshta vbscript:createobject("shell.application").shellexecute("""%~0""","::",,"runas",1)(window.close)&exit


rem --------------------------------------------------------------------------------------------------------------
rem 打开应用后返回到应用选择界面
:OpenApp
echo %all%|findstr /c:"%operate%" >nul && (
	set cuscmd=!%operate%!
	if "!cuscmd!" equ "" (
		echo [zq]$ [Error]: This command [%operate%] need a property execute path
	) else (
		echo [zq]$ !cuscmd! starting...
		rem 如果是应用程序路径，则使用start直接执行对应路径，否则，直接执行对应命令
		echo !cuscmd!|findstr ":" >nul && (
			for /l %%a in (1,1,%runTimes%) do (
				start "%operate%" !cuscmd!
			)
		) || (
			!cuscmd!
		)	
		echo [zq]$ !cuscmd! started...
	)
) || (
	cmd /c %operate%
	rem echo [zq]$ [Error]: Could not find the command : %operate%
	rem pause
)
goto ShowCmds


rem --------------------------------------------------------------------------------------------------------------
rem 打开应用后退出命令界面
:OpenAndExit
echo %operate%
echo %all%|findstr %operate% >nul && (
	set cuscmd=!%operate%!
	if "!cuscmd!" equ "" (
		echo [zq]$ [Error]: This command [%operate%] need a proper execute path
		pause
		goto ShowCmds
	) else (
		echo [zq]$ !cuscmd! starting...
		rem 如果是应用程序路径，则使用start直接执行对应路径，否则，直接执行对应命令
		echo !cuscmd!| findstr /c:":" >nul && (
			for /l %%a in (1,1,%runTimes%) do (
				start "%operate%" !cuscmd!
			)
		) || (
			!cuscmd!
		)	
		echo [zq]$ !cuscmd! started...
	)
	ping -n 1 127.0.0.1 >nul
	goto ExitCus
) || (
	cmd /c %operate%
	rem echo [zq]$ [Error]: Could not find the command : %operate%
	rem pause
	goto ShowCmds
)

  






rem 记录脚本修改历史
rem =============================================================================
goto modifyLog
	20160808--创建本自定义脚本，用于快速启动常用软件。
	20190408--命令路径配置文件先从用户目录取，如果没有则从命令执行的当前目录取。
	20190409--为特殊应用创建vbs脚本，该类应用启动后运行日志会在控制台输出，通过vbs脚本执行可以防止控制台关闭后程序退出的问题，脚本见下方附录。
	20190417--新增启动网络服务的判断，目前判断路径中是否存在":"，如果有则是程序，如果没有则是服务。
	20200411--新增允许同时启动多个应用进程，对应参数为runTimes，目前仅支持OpenAndExit方法。
	20200825--新增run as administrator操作，主要用于部分需要用administrator权限才能启动的windows服务。例如，net start mysql等。
	20210615--将路径配置文件中的"="都换为"=="，主要为了解决路径中可能带有参数（参数中有等号）而原先程序通过等号来判断自定义命令和命令路径。
	20230619--配置文件中如果需要指定启动目录，可以直接在"=="后加上 /d {path} {启动项}，注意这是start命令的固有属性，可以直接用
:modifyLog




rem 附录：
rem ==============================================================================
goto appendix
	附录1:
		配置文件:
			cusAppMapping.ini
		配置文件内容:
			work=="C:\Users\zhaoqi1\Desktop\work"
			ie=="C:\Program Files\Internet Explorer\iexplore.exe"
			mysql==net start mysql
			postman==C:\Users\zhaoqi1\AppData\Local\Postman\Update.exe --processStart "Postman.exe"
			jira==!chrome! "https://jira.shdev.net/secure/Tempo.jspa#/my-work/week?type=LIST"
			sd==/d "G:\Program Files\SD启动器安装\sd-webui-aki\sd-webui-aki-v4.1" "A启动器.exe"
		注意事项:
			路径中有空格时，需要前后加双引号
			路径中如果用到其他路径，则可以使用"!其他路径!"的方式进行引用

	附录2:
		特殊文件:
			龙信.vbs
		文件说明:
			由于某些程序启动后控制台会继续输出日志，导致关闭控制台后程序也会退出，使用vbs方式可以解决
		文件内容:
			Dim WinScriptHost 
			Set WinScriptHost = CreateObject("WScript.Shell") 
			WinScriptHost.Run Chr(34) & "D:\Program Files\龙信\龙信.exe" & Chr(34), 0 
			Set WinScriptHost = Nothing
:appendix
